module Bootstrap:List.

importModule Bootstrap:Bool.
importModule Bootstrap:Nat.
importModule Bootstrap:Product.

elet List (A : *) : * := ∀ X : *. X -> (A -> X -> X) -> X.

elet nil [A : *] : List A := Λ X : *. λ n : X. λ c : A -> X -> X. n.
elet cons [A : *] (a : A) (as : List A) : List A :=
  Λ X : *. λ n : X. λ c : A -> X -> X. c a (as ?X n c).

elet recursionList [A, X : *] (rn : X) (rc : A -> List A -> X -> X) (l : List A) : X :=
  pr2 ?_ ?X $ l ?(List A × X)
    (nil ?A, rn)
    (λ a : A. recursionProduct ?_ ?_ ?_ λ p1 : List A. λ p2 : X. (a ∷ p1), rc a p1 p2).

elet recursionList' [A, X : *] (rn : X) (rc : A -> X -> X) (l : List A) : X :=
  l ?X rn rc.

elet length [X : *] (l : List X) : Nat :=
  l ?Nat zero (λ x : X. λ n : Nat. suc n).

elet isNil [X : *] (l : List X) : Bool :=
  l ?Bool true (λ _ : X. λ _ : Bool. false).

elet listEq [X : *] (eqX : X -> X -> Bool) : List X -> List X -> Bool :=
  recursionList' ?_ ?_ (isNil ?X) $
    λ x : X. λ rec : List X -> Bool.
      recursionList ?_ ?_ false (λ y : X. λ ys : List X. λ _ : Bool. and (eqX x y) (rec ys)).

elet pureList [X : *] (x : X) : List X := [X|x].

elet app [X : *] (l1, l2 : List X) : List X :=
  l1 ?(List X) l2 (λ x : X. λ rec : List X. x ∷ rec).

elet snoc [X : *] (xs : List X) (x : X) : List X := app ?_ xs (pureList ?_ x).

elet filter [X : *] (f : X -> Bool) (l : List X) : List X :=
  l ?(List X) [X|]
    (λ x : X. λ rec : List X. ifthenelse ?_ (f x) (x ∷ rec) rec).

elet foldl [A, B : *] (f : B -> A -> B) (l : List A) : B -> B :=
  l ?(B -> B) (λ b : B. b) (λ a : A. λ rec : B -> B. λ b : B. f (rec b) a).

elet foldr [A, B : *] (f : A -> B -> B) (l : List A) (b : B) : B :=
  l ?B b (λ a : A. λ rec : B. f a rec).

let reverseAcc [X : *] (l : List X) : List X -> List X :=
  l ?(List X -> List X)
    (λ acc : List X. acc)
    (λ x : X. λ rec : List X -> List X. λ acc : List X. rec (cons ?X x acc)).

let reverse [X : *] (l : List X) : List X := reverseAcc ?X l [X|].

elet scanl [A, B : *] (f : B -> A -> B) (b : B) : List A -> List B :=
  recursionList ?_ ?_
    (pureList ?_ b)
    (λ a : A. λ as : List A. λ rec : List B. foldl ?_ ?_ f (a ∷ as) b ∷ rec).

elet map [X, Y : *] (f : X -> Y) (l : List X) : List Y :=
  l ?(List Y) [Y|] (λ x : X. λ rec : List Y. f x ∷ rec).

elet intersperse [X : *] (x : X) (l : List X) : List X :=
  recursionList ?_ ?_ [X|]
    (λ x' : X. λ l' : List X. λ rec : List X.
      ifthenelse ?_ (isNil ?_ l') (pureList ?_ x') (x' ∷ x ∷ rec)) l.

endModule.
