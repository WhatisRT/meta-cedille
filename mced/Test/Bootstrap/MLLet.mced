--------------------------------------------------------------------------------
-- ML-style let syntax
--
-- Use `b-let f (x1, ..., xn : T1) ... (... : Tn) : T := t.` to define a term
--------------------------------------------------------------------------------

module Bootstrap:MLLet.

Given M primLet primAnnLet.

importModule Bootstrap:Builtins.
importModule Bootstrap:Bool.
importModule Bootstrap:List.
importModule Bootstrap:Maybe.
importModule Bootstrap:Product.
importModule Bootstrap:Sum.
importModule Bootstrap:Term.
importModule Bootstrap:Unit.

-- Functions to turn a Telescope into an actual telescope that can be applied to
-- a term.

elet paramToLambda (t : Term) : Param -> Term :=
  recursionTripleProduct ?_ ?_ ?_ ?_ λ erased : Bool. λ name : String. λ type : Term.
    (ifthenelse ?_ erased LambdaTerm lambdaTerm) name type t.

elet foldWithLambdas : Telescope -> Term -> Term := foldl ?_ ?_ paramToLambda.

elet paramToPi (t : Term) : Param -> Term :=
  recursionTripleProduct ?_ ?_ ?_ ?_ λ erased : Bool. λ name : String. λ type : Term.
    (ifthenelse ?_ erased forallTerm piTerm) name type t.

elet foldWithPi : Telescope -> Term -> Term := foldl ?_ ?_ paramToPi.

-- LetInfo contains all the data associated to a `let`

elet LetInfo := TripleProduct String Term (Maybe Term).
elet mkLetInfo : String -> Term -> Maybe Term -> LetInfo :=
  mkTripleProduct ?String ?Term ?(Maybe Term).
elet letInfoName   (i : LetInfo) : String     := pr31 ?String ?Term ?(Maybe Term) i.
elet letInfoTerm   (i : LetInfo) : Term       := pr32 ?String ?Term ?(Maybe Term) i.
elet letInfoType   (i : LetInfo) : Maybe Term := pr33 ?String ?Term ?(Maybe Term) i.
elet letInfoHasAnn (i : LetInfo) : Bool       :=
  maybe ?_ ?_ false (λ _ : Term. true) (letInfoType i).

elet appendTelToLetInfo (tel : Telescope) : LetInfo -> LetInfo :=
  recursionTripleProduct ?String ?Term ?(Maybe Term) ?LetInfo
    λ n : String. λ t : Term. λ T : Maybe Term.
      mkLetInfo n (foldWithLambdas tel t) (mapMaybe ?_ ?_ (foldWithPi tel) T).

-- define the definition specified in `LetInfo`

elet convertLetInfo : LetInfo -> M Unit :=
  recursionTripleProduct ?_ ?_ ?_ ?_ λ n : String. λ t : Term. λ T : Maybe Term.
    maybe ?_ ?(M Unit)
      (primLet n (TermToInitTerm t))
      (λ type : Term. primAnnLet n (TermToInitTerm t) (TermToInitTerm type))
      T.

-- Syntax for `let`

elet init$betterLetType$ : Sum Term Unit := inr ?Term ?_ tt.
elet init$betterLetType$^space^_multiTerm_=colon= (t : Term) : Sum Term Unit :=
  inl ?_ ?Unit t.

elet init$betterLet$_string_^space^_telescope_=colon=_betterLetType_=equal=^space'^_multiTerm_=dot=
  (n : String) (tel : Telescope) (maybeType : Maybe Term) (term : Term) : LetInfo :=
  appendTelToLetInfo tel (mkLetInfo n term maybeType).

elet init$stmt'$b=minus=let^space^_betterLet_ : LetInfo -> M Unit := convertLetInfo.

endModule.
