let Iota := λ X : * λ P : Π _ : X * ι x : X [P x].

let iPair := Λ X : * Λ P : Π _ : X * λ x : X λ p : [P x] λ eq : (= p x)
  { x , (φ eq p x) y. [P y] }.

let iPr1 := Λ X : * Λ P : Π _ : X * λ x : [[Iota X] P] π x.
let iPr2 := Λ X : * Λ P : Π _ : X * λ x : [[Iota X] P] ψ x.

let castIota := Λ A : * Λ P : Π _ : A *
  <<<<intrCast [[Iota A] P]> A> <<iPr1 A> P>> <beta [[Iota A] P]>>
  : ∀ A : * ∀ P : Π _ : A * [[Cast [[Iota A] P]] A].

let iotaMono := λ F : Π _ : * * λ FP : Π X : * Π _ : [F X] *
  λ monoF : [Mono F]
  λ monoFP : ∀ Y1 : * ∀ Y2 : * Π c : [[Cast Y1] Y2] Π y : [F Y1]
    [[Cast [[FP Y1] y]] [[FP Y2] [<<<elimCast [F Y1]> [F Y2]> [<<monoF Y1> Y2> c]> y]]]
  Λ Y1 : * Λ Y2 : * λ c : [[Cast Y1] Y2]
  <<<<intrCast [[Iota [F Y1]] [FP Y1]]> [[Iota [F Y2]] [FP Y2]]>
    λ y : [[Iota [F Y1]] [FP Y1]]
      { [<<<elimCast [F Y1]> [F Y2]> [<<monoF Y1> Y2> c]> π y]
      , ([<<<elimCast [[FP Y1] π y]>
                      [[FP Y2] [<<<elimCast [F Y1]> [F Y2]> [<<monoF Y1> Y2> c]> π y]]>
                      [[<<monoFP Y1> Y2> c] π y]> ψ y]) x. [[FP Y2] x] }>
    λ y : [[Iota [F Y1]] [FP Y1]] β y y>.

let UEq := Λ X : * Λ Y : * λ x : X λ y : Y (= x y).

let usym := Λ X : * Λ x : X Λ y : X λ eq : (= x y) σ eq.
let usubst := Λ A : * Λ p : A Λ q : A Λ eq : (= p q) Λ P : Π _ : A * λ pf : [P q] ρ eq x. [P x] pf.
