--------------------------------------------------------------------------------
-- Base of the new syntax
--
-- We define some basic statements and the new syntax here
--------------------------------------------------------------------------------

b-let simpleCommand (s : String) : M metaResult :=
  mapMeta ?String ?metaResult strToMetaResult (shellCmd s (listToStringList [String |])).

b-let letInfoTermToQuoted (i : LetInfo) : LetInfo :=
  mkLetInfo (stringApp "qTerm" (letInfoName i)) (quoteTerm (letInfoTerm i)) (just ?Term θ{Term}).

b-let runMetaCommand (t : Term) : init$newStmt :=
  λ s : EvaluatorState.
    bindMeta ?init$newStmt ?(Product metaResult EvaluatorState) (checkTerm init$newStmt (TermToInitTerm t)) (λ stmt : init$newStmt. stmt s).

b-let runEcho (t : Term) : init$newStmt := runMetaCommand θ{echoStmt γ{t}}.

b-let reloadMEnv : init$newStmt := evalStateUnitToNewStmt (modifyEvalState (λ e : EvaluatorState. e)).

b-let showTypeStmt (t : Term) : init$newStmt :=
  bindEvalState ?Term ?metaResult (inferType' t) (λ x : Term. echoStmt (showTerm x)).

-- Syntax

-- statements from the old syntax
b-let init$newStmt'$o=minus=_stmt'_ (stmt : init$stmt') : init$newStmt := stmtToNewStmt stmt.

-- flags

b-let init$flag$true := true.
b-let init$flag$false := false.

-- set statements

b-let init$setStmt$RequireTypeAnnotations^space^_flag_ (b : Bool) :=
  seqEvalState ?Unit ?metaResult (modifyEvalFlags (modifyAnnReq b))
    (bindEvalState ?EvaluatorState ?metaResult getEvalState updateEvalState).

-- "native" statements in the new syntax
b-let init$newStmt'$set^space^_setStmt_=dot= (s : init$newStmt) := s.
b-let init$newStmt'$let^space^_betterLet_ (i : LetInfo) : init$newStmt := letInfoToNewStmtMod i.
b-let init$newStmt'$qlet^space^_betterLet_ (i : LetInfo) : init$newStmt :=
  seqNewStmt (letInfoToNewStmt i) (letInfoToNewStmt (letInfoTermToQuoted i)).
b-let init$newStmt'$runMeta^space^_multiTerm_=dot= (t : Term) : init$newStmt := runMetaCommand t.
b-let init$newStmt'$echo^space^_multiTerm_=dot= (t : Term) : init$newStmt :=
  bindEvalState ?Term ?metaResult (modifyTermMod t) runEcho.
b-let init$newStmt'$showType^space^_multiTerm_=dot= (t : Term) : init$newStmt := showTypeStmt t.
b-let init$newStmt'$import^space^_string_=dot= (n : String) : init$newStmt := stmtToNewStmt (quote$import' n).
b-let init$newStmt'$updateEval=dot= : init$newStmt := reloadMEnv.
b-let init$newStmt'$ : init$newStmt := emptyStmt.

b-let init$newStmt$^space'^_newStmt'_ (stmt : init$newStmt) : init$newStmt := stmt.

-- new evaluator
b-let evalNewStmt (s : EvaluatorState) (stmt : init$newStmt) : M (Product metaResult EvaluatorState) := stmt s.

seteval [evalNewStmt emptyEvaluatorState] init newStmt.

-- this defines the initial dictionary
runMeta stmtToNewStmt (convertLetInfo (mkLetInfo (mkModuleDictName emptyModuleDict) θ{emptyModuleDict} (just ?Term θ{ModuleDict}))).

-- Test parse:
--runMeta bindEvalState ?Term ?metaResult (inferType' θ{evalNewStmt}) (λ x : Term. echoStmt (showTerm x)).
--showType filter.