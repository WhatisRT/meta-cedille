--------------------------------------------------------------------------------
-- This file provides the data structures and functions for the theory of
-- cedille core extended with the constructs for metaprogramming.
--------------------------------------------------------------------------------

{-# OPTIONS --type-in-type #-}

module CoreTheory where

import Data.Product

open import Class.Map
open import Class.Monad.Except
open import Data.HSTrie
open import Data.Word using (toâ„•; fromâ„•)
open import Monads.Except

open import Prelude
open import Prelude.Nat

open import Theory.Names public
open import Theory.TermLike public
open import Theory.PrimMeta public
open import Theory.Terms public

private
  variable
    A B C : Set
    M : Set â†’ Set

data Def : Set where
  Let : AnnTerm â†’ AnnTerm â†’ Def
  Axiom : AnnTerm â†’ Def

instance
  Def-Show : Show Def
  Def-Show .show (Let x xâ‚) = " :=" <+> show x <+> ":" <+> show xâ‚
  Def-Show .show (Axiom x) = " :" <+> show x

private
  data EfficientDef : Set where
    EfficientLet : AnnTerm â†’ PureTerm â†’ AnnTerm â†’ EfficientDef
    EfficientAxiom : AnnTerm â†’ EfficientDef

  toDef : EfficientDef â†’ Def
  toDef (EfficientLet x xâ‚ xâ‚‚)   = Let x xâ‚‚
  toDef (EfficientAxiom x)       = Axiom x

  getNorm : EfficientDef â†’ Maybe PureTerm
  getNorm (EfficientLet x xâ‚ xâ‚‚) = return xâ‚
  getNorm (EfficientAxiom x)     = nothing

  typeOfDef : Def â†’ AnnTerm
  typeOfDef (Let _ x) = x
  typeOfDef (Axiom x) = x

GlobalContext : Set
GlobalContext = HSTrie EfficientDef

emptyGlobalContext : GlobalContext
emptyGlobalContext = emptyMap

Context : Set
Context = GlobalContext Ã— List AnnTerm

private
  instance
    Context-Show : Show Context
    Context-Show .show (fst , snd) = (show $ length snd) <+> "local variables:" + show snd

globalToContext : GlobalContext â†’ Context
globalToContext Î“ = Î“ , []

contextToGlobal : Context â†’ GlobalContext
contextToGlobal (fst , snd) = fst

private
  -- add variable to context, so that index 0 points to that variable
  pushVar : AnnTerm â†’ Context â†’ Context
  pushVar v (fst , snd) = fst , v âˆ· snd

  localContextLength : Context â†’ â„•
  localContextLength (fst , snd) = length snd

  efficientLookupInContext : Name â†’ Context â†’ Maybe EfficientDef
  efficientLookupInContext (Bound x) (fst , snd) =
    EfficientAxiom âˆ˜ weakenBy (sucğ•€ x) <$> lookupMaybe (toâ„• x) snd
  efficientLookupInContext (Free x) (fst , snd) = lookup x fst

  lookupInContext : Name â†’ Context â†’ Maybe Def
  lookupInContext n Î“ = toDef <$> efficientLookupInContext n Î“

  {-# TERMINATING #-}
  validInContext : PureTerm â†’ Context â†’ Bool
  validInContext = helper 0
    where
      -- instead of modifying the context here, we just count how many variables we would have added if we did
      helper : ğ•€ â†’ PureTerm â†’ Context â†’ Bool
      helper k (Var-P (Bound x)) Î“ = x <ğ•€ (fromâ„• (localContextLength Î“) +ğ•€ k)
      helper k (Var-P n@(Free x)) Î“ = maybe (Î» _ â†’ true) false $ lookupInContext n Î“
      helper k (Sort-P x) Î“ = true
      helper k (Const-P x) Î“ = true
      helper k (App-P t tâ‚) Î“ = helper k t Î“ âˆ§ helper k tâ‚ Î“
      helper k (Lam-P _ t) Î“ = helper (sucğ•€ k) t Î“
      helper k (Pi-P _ t tâ‚) Î“ = helper k t Î“ âˆ§ helper (sucğ•€ k) tâ‚ Î“
      helper k (All-P _ t tâ‚) Î“ = helper k t Î“ âˆ§ helper (sucğ•€ k) tâ‚ Î“
      helper k (Iota-P _ t tâ‚) Î“ = helper k t Î“ âˆ§ helper (sucğ•€ k) tâ‚ Î“
      helper k (Eq-P t tâ‚) Î“ = helper k t Î“ âˆ§ helper k tâ‚ Î“
      helper k (M-P t) Î“ = helper k t Î“
      helper k (Mu-P t tâ‚) Î“ = helper k t Î“ âˆ§ helper k tâ‚ Î“
      helper k (Epsilon-P t) Î“ = helper k t Î“
      helper k (Gamma-P t tâ‚) Î“ = helper k t Î“ âˆ§ helper k tâ‚ Î“
      helper k (Ev-P m t) Î“ = primMetaArgsAnd $ mapPrimMetaArgs (Î» x â†’ helper k x Î“) t
      helper k (Char-P c) Î“ = true
      helper k (CharEq-P t tâ‚) Î“ = helper k t Î“ âˆ§ helper k tâ‚ Î“

  {-# TERMINATING #-}
  checkFree : Name â†’ PureTerm â†’ Bool
  checkFree = helper 0
    where
      helper : ğ•€ â†’ Name â†’ PureTerm â†’ Bool
      helper k n (Var-P (Bound x)) = case n of Î» where
        (Bound xâ‚) â†’ x â‰£ (k +ğ•€ xâ‚)
        (Free xâ‚) â†’ false
      helper k n (Var-P (Free x)) = case n of Î» where
        (Bound xâ‚) â†’ false
        (Free xâ‚) â†’ x â‰£ xâ‚
      helper k n (Sort-P x) = false
      helper k n (Const-P x) = false
      helper k n (App-P t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Lam-P _ t) = helper (sucğ•€ k) n t
      helper k n (Pi-P _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (All-P _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (Iota-P _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (Eq-P t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (M-P t) = helper k n t
      helper k n (Mu-P t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Epsilon-P t) = helper k n t
      helper k n (Gamma-P t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Ev-P m t) = primMetaArgsAnd $ mapPrimMetaArgs (helper k n) t
      helper k n (Char-P c) = false
      helper k n (CharEq-P t tâ‚) = helper k n t âˆ§ helper k n tâ‚

-- something in is head normal form, if its outermost constructor is not one of the following: Var-A (if the lookup fails), App-A, AppE-A
{-# TERMINATING #-}
hnfNorm : Context â†’ AnnTerm â†’ AnnTerm
hnfNorm Î“ v@(Var-A x) with lookupInContext x Î“
... | just (Let xâ‚ xâ‚‚)  = hnfNorm Î“ xâ‚
... | just (Axiom xâ‚)   = v -- we cannot reduce axioms
... | nothing           = v -- in case the lookup fails, we cannot reduce
hnfNorm Î“ (App-A t tâ‚)  = maybe (Î» t' â†’ hnfNorm Î“ $ subst t' tâ‚) (t âŸª$âŸ« tâ‚) $ stripBinder (hnfNorm Î“ t)
hnfNorm Î“ (AppE-A t tâ‚) = maybe (Î» t' â†’ hnfNorm Î“ $ subst t' tâ‚) (t âŸª$âŸ« tâ‚) $ stripBinder (hnfNorm Î“ t)
{-# CATCHALL #-}
hnfNorm Î“ v             = v

hnfNormPure normalizePure : Context â†’ PureTerm â†’ PureTerm

{-# NON_TERMINATING #-}
hnfNormPure Î“ v@(Var-P x) with lookupInContext x Î“
... | just (Let xâ‚ xâ‚‚)         = hnfNormPure Î“ $ Erase xâ‚
... | just (Axiom xâ‚)          = v -- we cannot reduce axioms
... | nothing                  = v -- in case the lookup fails, we cannot reduce
hnfNormPure Î“ v@(App-P t tâ‚) with stripBinder (hnfNormPure Î“ t)
... | (just t')                = hnfNormPure Î“ $ subst t' tâ‚
... | nothing                  = v
hnfNormPure Î“ v@(CharEq-P _ _) = normalizePure Î“ v -- reduce to a bool, if possible
{-# CATCHALL #-}
hnfNormPure Î“ v                = v

{-# NON_TERMINATING #-}
normalizePure Î“ v@(Var-P x) with efficientLookupInContext x Î“
... | just (EfficientLet xâ‚ xâ‚‚ xâ‚ƒ) = xâ‚‚
... | just (EfficientAxiom xâ‚)     = v -- we cannot reduce axioms
... | nothing                      = v -- in case the lookup fails, we cannot reduce
normalizePure Î“ v@(Sort-P x)       = v
normalizePure Î“ v@(Const-P x)      = v
normalizePure Î“ (App-P t tâ‚) with hnfNormPure Î“ t
...| t' = case stripBinder t' of Î» where
    (just t'') â†’ normalizePure Î“ (subst t'' tâ‚)
    nothing    â†’ normalizePure Î“ t' âŸª$âŸ« normalizePure Î“ tâ‚
normalizePure Î“ (Lam-P n t) with normalizePure Î“ t
... | t''@(App-P t' (Var-P (Bound i))) = if i â‰£ 0 âˆ§ validInContext t' Î“
  then normalizePure Î“ (strengthen t') else Lam-P n t'' -- eta reduce here
... | t'' = Lam-P n t''
normalizePure Î“ (Pi-P n t tâ‚)      = Pi-P n (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (All-P n t tâ‚)     = All-P n (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (Iota-P n t tâ‚)    = Iota-P n (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (Eq-P t tâ‚)        = Eq-P (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (M-P t)            = M-P (normalizePure Î“ t)
normalizePure Î“ (Mu-P t tâ‚)        = Mu-P (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (Epsilon-P t)      = Epsilon-P (normalizePure Î“ t)
normalizePure Î“ (Gamma-P t tâ‚)     = Gamma-P (normalizePure Î“ t) (normalizePure Î“ tâ‚)
normalizePure Î“ (Ev-P m args)      = Ev-P m (mapPrimMetaArgs (normalizePure Î“) args)
normalizePure Î“ (Char-P c)         = (Char-P c)
normalizePure Î“ (CharEq-P t tâ‚) with normalizePure Î“ t | normalizePure Î“ tâ‚
... | (Char-P c) | (Char-P c')     = normalizePure Î“ $ FreeVar $ show (c â‰£ c')
{-# CATCHALL #-}
... | x | xâ‚                       = CharEq-P x xâ‚

insertInGlobalContext : GlobalName â†’ Def â†’ GlobalContext â†’ String âŠ GlobalContext
insertInGlobalContext n d Î“ =
  if is-just $ lookup n Î“
    then injâ‚ ("The name" <+> n <+> "is already defined!")
    else (injâ‚‚ $ insert n (toEfficientDef d Î“) Î“)
  where
    toEfficientDef : Def â†’ GlobalContext â†’ EfficientDef
    toEfficientDef (Let x xâ‚) Î“ = EfficientLet x (normalizePure (globalToContext Î“) $ Erase x) xâ‚
    toEfficientDef (Axiom x) Î“ = EfficientAxiom x

private
  beqMonadHelper : {{_ : EqB A}} {{_ : Show A}} {{_ : Monad M}} {{_ : MonadExcept M String}}
                â†’ A â†’ A â†’ String â†’ M âŠ¤
  beqMonadHelper a a' s =
    if a â‰£ a'
      then return tt
      else throwError (s <+> show a <+> "isn't equal to" <+> s <+> show a')

  {-# TERMINATING #-}
  pureTermBeq : {{_ : Monad M}} {{_ : MonadExcept M String}}
    â†’ PureTerm â†’ PureTerm â†’ M âŠ¤
  pureTermBeq (Var-P x) (Var-P xâ‚) = beqMonadHelper x xâ‚ "Name"
  pureTermBeq (Sort-P x) (Sort-P xâ‚) = beqMonadHelper x xâ‚ "Sort"
  pureTermBeq (Const-P x) (Const-P xâ‚) = beqMonadHelper x xâ‚ "Const"
  pureTermBeq (App-P t tâ‚) (App-P x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Lam-P _ t) (Lam-P _ tâ‚) = pureTermBeq t tâ‚
  pureTermBeq (Pi-P _ t tâ‚) (Pi-P _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (All-P _ t tâ‚) (All-P _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Iota-P _ t tâ‚) (Iota-P _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Eq-P t tâ‚) (Eq-P x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (M-P t) (M-P x) = pureTermBeq x t
  pureTermBeq (Mu-P t tâ‚) (Mu-P x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Epsilon-P t) (Epsilon-P x) = pureTermBeq t x
  pureTermBeq (Gamma-P t tâ‚) (Gamma-P x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Ev-P m t) (Ev-P m' x) with m â‰Ÿ m'
  ... | yes refl = void $ primMetaArgsSequence $ primMetaArgsZipWith pureTermBeq t x
  ... | no  _    = throwError $ show m <+> "and" <+> show m' <+> "aren't equal!"
  pureTermBeq (Char-P c) (Char-P c') = beqMonadHelper c c' "Char"
  pureTermBeq (CharEq-P t tâ‚) (CharEq-P x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  {-# CATCHALL #-}
  pureTermBeq t t' =
    throwError $ "The terms" <+> show t <+> "and" <+> show t' <+> "aren't equal!"

module _ {{_ : Monad M}} {{_ : MonadExcept M String}} (Î“ : Context) where
  compareNames : PureTerm â†’ PureTerm â†’ M âŠ¤
  compareNames (Var-P x) (Var-P xâ‚) =
    if x â‰£ xâ‚
      then return tt
      else throwError "Names not equal! If you see this message, this is a bug!"
  {-# CATCHALL #-}
  compareNames _ _ = throwError "Terms are not names! If you see this message, this is a bug!"

  {-# NON_TERMINATING #-}
  checkÎ²Î·Pure : PureTerm â†’ PureTerm â†’ M âŠ¤
  checkÎ²Î·Pure t t' =
    tryElse (compareNames t t') $
    compareHnfs (hnfNormPure Î“ t) (hnfNormPure Î“ t')
    -- tryElse (compareHnfs (hnfNormPure Î“ t) (hnfNormPure Î“ t')) $
    -- pureTermBeq t t'
    where
      hnfError : PureTerm â†’ PureTerm â†’ M âŠ¤
      hnfError t t' =
        throwError $ "The terms" <+> show t <+> "and" <+> show t' <+> "aren't equal!"

      compareHnfs : PureTerm â†’ PureTerm â†’ M âŠ¤
      compareHnfs (Var-P x) (Var-P xâ‚) = beqMonadHelper x xâ‚ "Name"
      compareHnfs (Sort-P x) (Sort-P xâ‚) = beqMonadHelper x xâ‚ "Sort"
      compareHnfs (Const-P x) (Const-P xâ‚) = beqMonadHelper x xâ‚ "Const"
      compareHnfs (App-P t tâ‚) (App-P x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Lam-P _ t) (Lam-P _ tâ‚) = checkÎ²Î·Pure t tâ‚
      compareHnfs (Pi-P _ t tâ‚) (Pi-P _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (All-P _ t tâ‚) (All-P _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Iota-P _ t tâ‚) (Iota-P _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Eq-P t tâ‚) (Eq-P x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (M-P t) (M-P x) = checkÎ²Î·Pure x t
      compareHnfs (Mu-P t tâ‚) (Mu-P x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Epsilon-P t) (Epsilon-P x) = checkÎ²Î·Pure t x
      compareHnfs (Gamma-P t tâ‚) (Gamma-P x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs t@(Ev-P m x) t'@(Ev-P m' x') with m â‰Ÿ m'
      ... | yes refl = void $ primMetaArgsSequence $ primMetaArgsZipWith checkÎ²Î·Pure x x'
      ... | no  _    = hnfError t t'
      compareHnfs (Char-P c) (Char-P c') = beqMonadHelper c c' "Char"
      compareHnfs (CharEq-P t tâ‚) (CharEq-P x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Lam-P _ t) tâ‚ = case normalizePure Î“ t of Î» where
        t''@(App-P t' (Var-P (Bound i))) â†’ if i â‰£ 0 âˆ§ validInContext t' Î“
          then (compareHnfs (strengthen t') tâ‚) else hnfError t'' tâ‚
        t'' â†’ hnfError t'' tâ‚
      compareHnfs t (Lam-P _ tâ‚) = case normalizePure Î“ tâ‚ of Î» where
        t''@(App-P t' (Var-P (Bound i))) â†’ if i â‰£ 0 âˆ§ validInContext t' Î“
          then (compareHnfs t (strengthen t')) else hnfError t t''
        t'' â†’ hnfError t t''
      {-# CATCHALL #-}
      compareHnfs t t' = hnfError t t'

  checkÎ²Î· : AnnTerm â†’ AnnTerm â†’ M âŠ¤
  checkÎ²Î· t t' = checkÎ²Î·Pure (Erase t) (Erase t')

{-# TERMINATING #-}
synthType synthType' :
  {{_ : Monad M}} {{_ : MonadExcept M String}} â†’ Context â†’ AnnTerm â†’ M AnnTerm

synthType Î“ t =
  appendIfError (synthType' Î“ t) $
    "\n\nWhile synthesizing type for" <+> shortenString 1000 (show t) <+> "in context:\n" + show {{Context-Show}} Î“

synthType' Î“ (Var-A x) =
  maybeToError
    (typeOfDef <$> lookupInContext x Î“)
    ("Lookup failed:" <+> show x <+> "in context" <+> show {{Context-Show}} Î“)
synthType' Î“ (Sort-A Ast) = return â–¡
synthType' Î“ (Sort-A Sq) = throwError "Cannot synthesize type for the superkind"

synthType' Î“ (Const-A CharT) = return â‹†

synthType' Î“ (Pr1-A t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Iota-A _ u uâ‚) â†’ return u
    ; _ â†’ throwError "Term does not normalize to an iota term" }

synthType' Î“ (Pr2-A t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Iota-A _ u uâ‚) â†’ return $ subst uâ‚ (Pr1-A t)
    ; _ â†’ throwError "Term does not normalize to an iota term" }

synthType' Î“ (Beta-A t tâ‚) = do
  T â† synthType Î“ (Eq-A t t)
  case (hnfNorm Î“ T) of Î»
    { (Sort-A Ast) â†’ return $ Eq-A t t
    ; _ â†’ throwError "Equality type does not have the right type. Is this a bug?" }

synthType' Î“ (Delta-A t tâ‚) = do
  T â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (Eq-A u uâ‚) â†’ do
      catchError
        (pureTermBeq (normalizePure Î“ $ Erase u) (Lam-P "" $ Lam-P "" $ BoundVar 1) >>
         pureTermBeq (normalizePure Î“ $ Erase uâ‚) (Lam-P "" $ Lam-P "" $ BoundVar 0))
        (Î» e â†’ throwError $
          "This equality cannot be used for the delta term:" <+> show u
          <+> "=" <+> show uâ‚ + "\nError:" <+> e)
      return t
    ; _ â†’ throwError "The second argument of a delta needs to be of an eq type" }

synthType' Î“ (Sigma-A t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Eq-A u uâ‚) â†’ return $ Eq-A uâ‚ u
    ; _ â†’ throwError "Sigma needs an inhabitant of an eq type as argument" }

synthType' Î“ (App-A t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (Pi-A _ u uâ‚) â†’ do
      catchError
        (checkÎ²Î· Î“ Tâ‚ u)
        (Î» e â†’ throwError ("Type mismatch in application, the type of" <+> show tâ‚
          <+> ":" <+> show Tâ‚ +  " is not Î²Î·-equivalent to" <+> show u + "\nError:" <+> e))
      return $ subst uâ‚ tâ‚
    ; v â†’ throwError $
      "The left term in an application needs to have a pi type, while it has type" <+> show v }

synthType' Î“ (AppE-A t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (All-A _ u uâ‚) â†’ do
      catchError (checkÎ²Î· Î“ u Tâ‚)
        (Î» e â†’ throwError
          ("Type mismatch in erased application, the following types are not Î²Î·-equivalent:\n"
            + show u + "\n" + show Tâ‚ + "\nError:\n" + e))
      return $ subst uâ‚ tâ‚
    ; v â†’ throwError $
      "The left term in an erased application needs to have a forall type, while it has type "
        + show v + "\nTest:" <+> show T }

synthType' Î“ (Rho-A t tâ‚ tâ‚‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚‚
  case (hnfNorm Î“ T) of Î»
    { (Eq-A u uâ‚) â†’ do
      catchError (checkÎ²Î· Î“ (subst tâ‚ uâ‚) Tâ‚)
        (Î» e â†’ throwError $ "Type mismatch in rho:" <+> show (subst tâ‚ uâ‚)
          <+> "should be Î²Î·-equivalent to the synthesized type of" <+> show tâ‚‚ <+> ": "
          + show Tâ‚ + "\nError:\n" + e)
      return $ subst tâ‚ u
    ; _ â†’ throwError "The type of the first argument of a rho needs to be an equality" }

synthType' Î“ (All-A _ t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-A s) â†’ do
      let Î“' = pushVar t Î“
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“' uâ‚) of Î»
        { (Sort-A Ast) â†’ return â‹†
        ; v â†’ throwError $
          "The type family in forall should have type star, while it has type "
          + show v <+> "(" + show tâ‚ + ")\nContext:" <+> show {{Context-Show}} Î“' }
    ; _ â†’ throwError "The type of the parameter type in forall should be star or square" }

synthType' Î“ (Pi-A _ t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-A s) â†’ do
      let Î“' = pushVar t Î“
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“ uâ‚) of Î»
        { (Sort-A s') â†’ return $ Sort-A s'
        ; v â†’ throwError $
          "The type family in pi should have type star or square, while it has type" <+> show v }
    ; _ â†’ throwError "The type of the parameter type in pi should be star or square" }

synthType' Î“ (Iota-A _ t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-A Ast) â†’ do
      let Î“' = pushVar t Î“
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“' uâ‚) of Î»
        { (Sort-A Ast) â†’ return â‹†
        ; _ â†’ throwError "The type family in iota should have type star"}
    ; _ â†’ throwError "The type of the parameter type in iota should be star" }

synthType' Î“ (Lam-A n t tâ‚) = do
  synthType Î“ t
  u â† synthType (pushVar t Î“) tâ‚
  return (Pi-A n t u)

synthType' Î“ (LamE-A n t tâ‚) =
  if checkFree (Bound 0) (Erase tâ‚)
    then throwError "Erased arguments cannot appear bound in a term"
    else do
      synthType Î“ t
      u â† synthType (pushVar t Î“) tâ‚
      return $ All-A n t u

synthType' Î“ (Pair-A t tâ‚ tâ‚‚) = do
  catchError (checkÎ²Î· Î“ t tâ‚)
    (Î» e â†’ throwError $
      "The terms in dependent intersection introduction have to be Î²Î·-equivalent. They normalize to:\n"
        + show (normalizePure Î“ $ Erase t) + "\n"
        + show (normalizePure Î“ $ Erase tâ‚) + "\nError:\n" + e)
  u â† synthType Î“ t
  uâ‚ â† synthType Î“ tâ‚
  catchError
    (checkÎ²Î· Î“ (subst tâ‚‚ t) uâ‚)
    (Î» e â†’ throwError
      ("Type mismatch in the second argument of the dependent intersection: "
        + show (subst tâ‚‚ t) <+> "should be Î²Î·-equivalent to the synthesized type "
        + show uâ‚ + "\n" + e))
  let res = Iota-A "" u tâ‚‚
  uâ‚‚ â† synthType Î“ res
  case (hnfNorm Î“ uâ‚‚) of Î»
    { (Sort-A Ast) â†’ return res
    ; _ â†’ throwError
      "The resulting iota type of the dependent intersection doesn't have type star. Is this a Bug?" }

synthType' Î“ (Phi-A t tâ‚ tâ‚‚) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Eq-A u uâ‚) â†’ do
      catchError
        (checkÎ²Î· Î“ tâ‚ u >> checkÎ²Î· Î“ tâ‚‚ uâ‚)
        (Î» e â†’ throwError $
          "The arguments to phi are not equivalent to the sides of the equality. Error:\n" + e)
      synthType Î“ tâ‚
    ; _ â†’ throwError "The first argument to phi should be of an equality type" }

synthType' Î“ (Eq-A x xâ‚) =
  if validInContext (Erase x) Î“
    then if validInContext (Erase xâ‚) Î“
      then return â‹†
      else throwError
        ("The right term in the equality type needs to be valid in the context:" <+> show xâ‚)
    else throwError
      ("The left term in the equality type needs to be valid in the context:" <+> show x)

synthType' Î“ (M-A t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Sort-A Ast) â†’ return â‹†
    ; _ â†’ throwError "The term M is applied to should have type âˆ—"}

synthType' Î“ (Mu-A t tâ‚) = do
  T â† synthType Î“ t
  T' â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (M-A u) â†’
      case (hnfNorm Î“ T') of Î»
        { (Pi-A _ v vâ‚) â†’ do
          T'' â† if checkFree (Bound 0) (Erase vâ‚)
            then throwError ("Index 0 is not allowed to appear in" <+> show vâ‚)
            else synthType (pushVar v Î“) vâ‚
          case (hnfNorm Î“ T'') of Î»
            { (Sort-A âˆ—) â†’
              case (hnfNorm Î“ vâ‚) of Î»
                { (M-A vâ‚‚) â†’
                  appendIfError
                    (checkÎ²Î· Î“ u v)
                    "The types in Î¼ need to be compatible" >> return (M-A $ strengthen vâ‚‚)
                ; _ â†’ throwError
                  "The second term in a Î¼ needs to have a Pi type that maps to 'M t' for some 't'" }
            ; _ â†’ throwError "The second term in a Î¼ needs to have a non-dependent Pi type" }
        ; _ â†’ throwError "The second term in a Î¼ needs to have a Pi type" }
    ; _ â†’ throwError "The first term in a Î¼ needs to have type 'M t' for some 't'" }

synthType' Î“ (Epsilon-A t) = M-A <$> synthType Î“ t

synthType' Î“ (Ev-A m t) = do
  T â† traversePrimMetaArgs (synthType Î“) t
  appendIfError
    (primMetaArgsSequence $ primMetaArgsZipWith (checkÎ²Î· Î“) T $ primMetaS m)
    ("The arguments for primitive" <+> show m <+> "have incorrect types!")
  return $ M-A $ primMetaT m t

synthType' Î“ (Gamma-A t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (M-A u) â†’ do
      appendIfError (checkÎ²Î· Î“ Tâ‚ (Pi-A "" (FreeVar "init$err") (weakenBy 1 $ M-A u)))
        ("The second term supplied to CatchErr has type" <+> show Tâ‚ +
         ", while it should have type 'init$err â†’ M" <+> show u)
      return $ M-A u
    ; _ â†’ throwError "The first term in CatchErr needs to have type 'M t' for some 't'" }

synthType' Î“ (Char-A c) = return (Const-A CharT)
synthType' Î“ (CharEq-A t t') = do
  T â† synthType Î“ t
  T' â† synthType Î“ t'
  case (hnfNorm Î“ T) of Î»
    { (Const-A CharT) â†’ case (hnfNorm Î“ T') of Î»
      { (Const-A CharT) â†’ return $ FreeVar "Bool"
      ; _ â†’ throwError "The second term in CharEq needs to have type Char" }
    ; _ â†’ throwError "The first term in CharEq needs to have type Char" }
