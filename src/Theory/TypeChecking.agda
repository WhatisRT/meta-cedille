--------------------------------------------------------------------------------
-- This file provides the data structures and functions for the theory of
-- cedille core extended with the constructs for metaprogramming.
--------------------------------------------------------------------------------

{-# OPTIONS --type-in-type #-}

module Theory.TypeChecking where

import Data.Product

open import Data.HSTrie
open import Class.Map
open import Class.Monad.Except
open import Data.Word using (toâ„•; fromâ„•)
open import Monads.Except

open import Prelude
open import Prelude.Nat

open import Theory.Names public
open import Theory.TermLike public
open import Theory.PrimMeta public
open import Theory.Terms hiding (PureTerm) public
import Theory.NBE as NBE
open import Theory.Context public
open import Theory.Normalisation public
open import Theory.NBE using (genExtra)

module _ {a b : Bool} where
  open Types (Term-TermLike {a} {b}) public

PureTerm : Set
PureTerm = Theory.Terms.PureTerm false

private
  variable
    A B C : Set
    M : Set â†’ Set

  {-# TERMINATING #-}
  checkFree : Name â†’ PureTerm â†’ Bool
  checkFree = helper 0
    where
      helper : ğ•€ â†’ Name â†’ PureTerm â†’ Bool
      helper k n (Var-T (Bound x)) = case n of Î» where
        (Bound xâ‚) â†’ x â‰£ (k +ğ•€ xâ‚)
        (Free xâ‚) â†’ false
      helper k n (Var-T (Free x)) = case n of Î» where
        (Bound xâ‚) â†’ false
        (Free xâ‚) â†’ x â‰£ xâ‚
      helper k n (Sort-T x) = false
      helper k n (Const-T x) = false
      helper k n (App t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Lam-P _ t) = helper (sucğ•€ k) n t
      helper k n (Pi _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (All _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (Iota _ t tâ‚) = helper k n t âˆ§ helper (sucğ•€ k) n tâ‚
      helper k n (Eq-T t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (M-T t) = helper k n t
      helper k n (Mu t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Epsilon t) = helper k n t
      helper k n (Gamma t tâ‚) = helper k n t âˆ§ helper k n tâ‚
      helper k n (Ev m t) = primMetaArgsAnd $ mapPrimMetaArgs (helper k n) t
      helper k n (Char-T c) = false
      helper k n (CharEq t tâ‚) = helper k n t âˆ§ helper k n tâ‚

insertInGlobalContext : GlobalName â†’ Def â†’ GlobalContext â†’ String âŠ GlobalContext
insertInGlobalContext n d Î“ =
  if is-just $ lookup n Î“
    then injâ‚ ("The name" <+> n <+> "is already defined!")
    else (injâ‚‚ $ insert n (toEfficientDef d Î“) Î“)
  where
    toEfficientDef : Def â†’ GlobalContext â†’ Def
    toEfficientDef d@(â‰” x) Î“ = record d { extra = just $ genExtra (globalToContext Î“) $ Erase x }
    toEfficientDef d Î“ = d

private
  beqMonadHelper : {{_ : EqB A}} {{_ : Show A}} {{_ : Monad M}} {{_ : MonadExcept M String}}
                â†’ A â†’ A â†’ String â†’ M âŠ¤
  beqMonadHelper a a' s =
    if a â‰£ a'
      then return tt
      else throwError (s <+> show a <+> "isn't equal to" <+> s <+> show a')

  {-# TERMINATING #-}
  pureTermBeq : {{_ : Monad M}} {{_ : MonadExcept M String}}
    â†’ PureTerm â†’ PureTerm â†’ M âŠ¤
  pureTermBeq (Var-T x) (Var-T xâ‚) = beqMonadHelper x xâ‚ "Name"
  pureTermBeq (Sort-T x) (Sort-T xâ‚) = beqMonadHelper x xâ‚ "Sort"
  pureTermBeq (Const-T x) (Const-T xâ‚) = beqMonadHelper x xâ‚ "Const"
  pureTermBeq (App t tâ‚) (App x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Lam-P _ t) (Lam-P _ tâ‚) = pureTermBeq t tâ‚
  pureTermBeq (Pi _ t tâ‚) (Pi _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (All _ t tâ‚) (All _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Iota _ t tâ‚) (Iota _ x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Eq-T t tâ‚) (Eq-T x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (M-T t) (M-T x) = pureTermBeq x t
  pureTermBeq (Mu t tâ‚) (Mu x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Epsilon t) (Epsilon x) = pureTermBeq t x
  pureTermBeq (Gamma t tâ‚) (Gamma x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  pureTermBeq (Ev m t) (Ev m' x) with m â‰Ÿ m'
  ... | yes refl = void $ primMetaArgsSequence $ primMetaArgsZipWith pureTermBeq t x
  ... | no  _    = throwError $ show m <+> "and" <+> show m' <+> "aren't equal!"
  pureTermBeq (Char-T c) (Char-T c') = beqMonadHelper c c' "Char"
  pureTermBeq (CharEq t tâ‚) (CharEq x xâ‚) = pureTermBeq t x >> pureTermBeq tâ‚ xâ‚
  {-# CATCHALL #-}
  pureTermBeq t t' =
    throwError $ "The terms" <+> show t <+> "and" <+> show t' <+> "aren't equal!"

module _ {{_ : Monad M}} {{_ : MonadExcept M String}} (Î“ : Context) where
  compareNames : PureTerm â†’ PureTerm â†’ M âŠ¤
  compareNames (Var-T x) (Var-T xâ‚) =
    if x â‰£ xâ‚
      then return tt
      else throwError "Names not equal! If you see this message, this is a bug!"
  {-# CATCHALL #-}
  compareNames _ _ = throwError "Terms are not names! If you see this message, this is a bug!"

  {-# NON_TERMINATING #-}
  checkÎ²Î·Pure : PureTerm â†’ PureTerm â†’ M âŠ¤
  checkÎ²Î·Pure t t' =
    tryElse (compareNames t t') $
    compareHnfs (hnfNormPure Î“ t) (hnfNormPure Î“ t')
    -- tryElse (compareHnfs (hnfNormPure Î“ t) (hnfNormPure Î“ t')) $
    -- pureTermBeq t t'
    where
      hnfError : PureTerm â†’ PureTerm â†’ M âŠ¤
      hnfError t t' =
        throwError $ "The terms" <+> show t <+> "and" <+> show t' <+> "aren't equal!"

      compareHnfs : PureTerm â†’ PureTerm â†’ M âŠ¤
      compareHnfs (Var-T x) (Var-T xâ‚) = beqMonadHelper x xâ‚ "Name"
      compareHnfs (Sort-T x) (Sort-T xâ‚) = beqMonadHelper x xâ‚ "Sort-T"
      compareHnfs (Const-T x) (Const-T xâ‚) = beqMonadHelper x xâ‚ "Const"
      compareHnfs (App t tâ‚) (App x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Lam-P _ t) (Lam-P _ tâ‚) = checkÎ²Î·Pure t tâ‚
      compareHnfs (Pi _ t tâ‚) (Pi _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (All _ t tâ‚) (All _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Iota _ t tâ‚) (Iota _ x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Eq-T t tâ‚) (Eq-T x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (M-T t) (M-T x) = checkÎ²Î·Pure x t
      compareHnfs (Mu t tâ‚) (Mu x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Epsilon t) (Epsilon x) = checkÎ²Î·Pure t x
      compareHnfs (Gamma t tâ‚) (Gamma x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs t@(Ev m x) t'@(Ev m' x') with m â‰Ÿ m'
      ... | yes refl = void $ primMetaArgsSequence $ primMetaArgsZipWith checkÎ²Î·Pure x x'
      ... | no  _    = hnfError t t'
      compareHnfs (Char-T c) (Char-T c') = beqMonadHelper c c' "Char"
      compareHnfs (CharEq t tâ‚) (CharEq x xâ‚) = checkÎ²Î·Pure t x >> checkÎ²Î·Pure tâ‚ xâ‚
      compareHnfs (Lam-P _ t) tâ‚ = case normalizePure Î“ t of Î» where
        t''@(App t' (Var-T (Bound i))) â†’ if i â‰£ 0 âˆ§ validInContext t' Î“
          then (compareHnfs (strengthen t') tâ‚) else hnfError t'' tâ‚
        t'' â†’ hnfError t'' tâ‚
      compareHnfs t (Lam-P _ tâ‚) = case normalizePure Î“ tâ‚ of Î» where
        t''@(App t' (Var-T (Bound i))) â†’ if i â‰£ 0 âˆ§ validInContext t' Î“
          then (compareHnfs t (strengthen t')) else hnfError t t''
        t'' â†’ hnfError t t''
      {-# CATCHALL #-}
      compareHnfs t t' = hnfError t t'

  checkÎ²Î· : AnnTerm â†’ AnnTerm â†’ M âŠ¤
  checkÎ²Î· t t' = checkÎ²Î·Pure (Erase t) (Erase t')

{-# TERMINATING #-}
synthType synthType' :
  {{_ : Monad M}} {{_ : MonadExcept M String}} â†’ Context â†’ AnnTerm â†’ M AnnTerm

synthType Î“ t =
  appendIfError (synthType' Î“ t) $
    "\n\nWhile synthesizing type for" <+> shortenString 1000 (show t) <+> "in context:\n" + show {{Context-Show}} Î“

synthType' Î“ (Var-T x) =
  maybeToError
    (lookupTypeInContext x Î“)
    ("Lookup failed:" <+> show x <+> "in context" <+> show {{Context-Show}} Î“)
synthType' Î“ (Sort-T Ast) = return â–¡
synthType' Î“ (Sort-T Sq) = throwError "Cannot synthesize type for the superkind"

synthType' Î“ (Const-T CharT) = return â‹†

synthType' Î“ (Pr1 t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Iota _ u uâ‚) â†’ return u
    ; _ â†’ throwError "Term does not normalize to an iota term" }

synthType' Î“ (Pr2 t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Iota _ u uâ‚) â†’ return $ subst uâ‚ (Pr1 t)
    ; _ â†’ throwError "Term does not normalize to an iota term" }

synthType' Î“ (Beta t tâ‚) = do
  T â† synthType Î“ (Eq-T t t)
  case (hnfNorm Î“ T) of Î»
    { (Sort-T Ast) â†’ return $ Eq-T t t
    ; _ â†’ throwError "Equality type does not have the right type. Is this a bug?" }

synthType' Î“ (Delta t tâ‚) = do
  T â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (Eq-T u uâ‚) â†’ do
      catchError
        (pureTermBeq (normalizePure Î“ $ Erase u) (Lam-P "" $ Lam-P "" $ BoundVar 1) >>
         pureTermBeq (normalizePure Î“ $ Erase uâ‚) (Lam-P "" $ Lam-P "" $ BoundVar 0))
        (Î» e â†’ throwError $
          "This equality cannot be used for the delta term:" <+> show u
          <+> "=" <+> show uâ‚ + "\nError:" <+> e)
      return t
    ; _ â†’ throwError "The second argument of a delta needs to be of an eq type" }

synthType' Î“ (Sigma t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Eq-T u uâ‚) â†’ return $ Eq-T uâ‚ u
    ; _ â†’ throwError "Sigma needs an inhabitant of an eq type as argument" }

synthType' Î“ (App t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (Pi _ u uâ‚) â†’ do
      catchError
        (checkÎ²Î· Î“ Tâ‚ u)
        (Î» e â†’ throwError ("Type mismatch in application, the type of" <+> show tâ‚
          <+> ":" <+> show Tâ‚ +  " is not Î²Î·-equivalent to" <+> show u + "\nError:" <+> e))
      return $ subst uâ‚ tâ‚
    ; v â†’ throwError $
      "The left term in an application needs to have a pi type, while it has type" <+> show v }

synthType' Î“ (AppE t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (All _ u uâ‚) â†’ do
      catchError (checkÎ²Î· Î“ u Tâ‚)
        (Î» e â†’ throwError
          ("Type mismatch in erased application, the following types are not Î²Î·-equivalent:\n"
            + show u + "\n" + show Tâ‚ + "\nError:\n" + e))
      return $ subst uâ‚ tâ‚
    ; v â†’ throwError $
      "The left term in an erased application needs to have a forall type, while it has type "
        + show v + "\nTest:" <+> show T }

synthType' Î“ (Rho t tâ‚ tâ‚‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚‚
  case (hnfNorm Î“ T) of Î»
    { (Eq-T u uâ‚) â†’ do
      catchError (checkÎ²Î· Î“ (subst tâ‚ uâ‚) Tâ‚)
        (Î» e â†’ throwError $ "Type mismatch in rho:" <+> show (subst tâ‚ uâ‚)
          <+> "should be Î²Î·-equivalent to the synthesized type of" <+> show tâ‚‚ <+> ": "
          + show Tâ‚ + "\nError:\n" + e)
      return $ subst tâ‚ u
    ; _ â†’ throwError "The type of the first argument of a rho needs to be an equality" }

synthType' Î“ (All n t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-T s) â†’ do
      let Î“' = pushType Î“ (n , t)
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“' uâ‚) of Î»
        { (Sort-T Ast) â†’ return â‹†
        ; v â†’ throwError $
          "The type family in forall should have type star, while it has type "
          + show v <+> "(" + show tâ‚ + ")\nContext:" <+> show {{Context-Show}} Î“' }
    ; _ â†’ throwError "The type of the parameter type in forall should be star or square" }

synthType' Î“ (Pi n t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-T s) â†’ do
      let Î“' = pushType Î“ (n , t)
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“ uâ‚) of Î»
        { (Sort-T s') â†’ return $ Sort-T s'
        ; v â†’ throwError $
          "The type family in pi should have type star or square, while it has type" <+> show v }
    ; _ â†’ throwError "The type of the parameter type in pi should be star or square" }

synthType' Î“ (Iota n t tâ‚) = do
  u â† synthType Î“ t
  case (hnfNorm Î“ u) of Î»
    { (Sort-T Ast) â†’ do
      let Î“' = pushType Î“ (n , t)
      uâ‚ â† synthType Î“' tâ‚
      case (hnfNorm Î“' uâ‚) of Î»
        { (Sort-T Ast) â†’ return â‹†
        ; _ â†’ throwError "The type family in iota should have type star"}
    ; _ â†’ throwError "The type of the parameter type in iota should be star" }

synthType' Î“ (Lam-A n t tâ‚) = do
  synthType Î“ t
  u â† synthType (pushType Î“ (n , t)) tâ‚
  return (Pi n t u)

synthType' Î“ (LamE n t tâ‚) =
  if checkFree (Bound 0) (Erase tâ‚)
    then throwError "Erased arguments cannot appear bound in a term"
    else do
      synthType Î“ t
      u â† synthType (pushType Î“ (n , t)) tâ‚
      return $ All n t u

synthType' Î“ (Pair t tâ‚ tâ‚‚) = do
  catchError (checkÎ²Î· Î“ t tâ‚)
    (Î» e â†’ throwError $
      "The terms in dependent intersection introduction have to be Î²Î·-equivalent. They normalize to:\n"
        + show (normalizePure Î“ $ Erase t) + "\n"
        + show (normalizePure Î“ $ Erase tâ‚) + "\nError:\n" + e)
  u â† synthType Î“ t
  uâ‚ â† synthType Î“ tâ‚
  catchError
    (checkÎ²Î· Î“ (subst tâ‚‚ t) uâ‚)
    (Î» e â†’ throwError
      ("Type mismatch in the second argument of the dependent intersection: "
        + show (subst tâ‚‚ t) <+> "should be Î²Î·-equivalent to the synthesized type "
        + show uâ‚ + "\n" + e))
  let res = Iota "" u tâ‚‚
  uâ‚‚ â† synthType Î“ res
  case (hnfNorm Î“ uâ‚‚) of Î»
    { (Sort-T Ast) â†’ return res
    ; _ â†’ throwError
      "The resulting iota type of the dependent intersection doesn't have type star. Is this a Bug?" }

synthType' Î“ (Phi t tâ‚ tâ‚‚) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Eq-T u uâ‚) â†’ do
      catchError
        (checkÎ²Î· Î“ tâ‚ u >> checkÎ²Î· Î“ tâ‚‚ uâ‚)
        (Î» e â†’ throwError $
          "The arguments to phi are not equivalent to the sides of the equality. Error:\n" + e)
      synthType Î“ tâ‚
    ; _ â†’ throwError "The first argument to phi should be of an equality type" }

synthType' Î“ (Eq-T x xâ‚) =
  if validInContext (Erase x) Î“
    then if validInContext (Erase xâ‚) Î“
      then return â‹†
      else throwError
        ("The right term in the equality type needs to be valid in the context:" <+> show xâ‚)
    else throwError
      ("The left term in the equality type needs to be valid in the context:" <+> show x)

synthType' Î“ (M-T t) = do
  T â† synthType Î“ t
  case (hnfNorm Î“ T) of Î»
    { (Sort-T Ast) â†’ return â‹†
    ; _ â†’ throwError "The term M is applied to should have type âˆ—"}

synthType' Î“ (Mu t tâ‚) = do
  T â† synthType Î“ t
  T' â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (M-T u) â†’
      case (hnfNorm Î“ T') of Î»
        { (Pi _ v vâ‚) â†’ do
          T'' â† if checkFree (Bound 0) (Erase vâ‚)
            then throwError ("Index 0 is not allowed to appear in" <+> show vâ‚)
            else synthType (pushType Î“ ("_" , v)) vâ‚
          case (hnfNorm Î“ T'') of Î»
            { (Sort-T âˆ—) â†’
              case (hnfNorm Î“ vâ‚) of Î»
                { (M-T vâ‚‚) â†’
                  appendIfError
                    (checkÎ²Î· Î“ u v)
                    "The types in Î¼ need to be compatible" >> return (M-T $ strengthen vâ‚‚)
                ; _ â†’ throwError
                  "The second term in a Î¼ needs to have a Pi type that maps to 'M t' for some 't'" }
            ; _ â†’ throwError "The second term in a Î¼ needs to have a non-dependent Pi type" }
        ; _ â†’ throwError "The second term in a Î¼ needs to have a Pi type" }
    ; t â†’ throwError ("The first term in a Î¼ needs to have type 'M t' for some 't'. It has type " <+> show t) }

synthType' Î“ (Epsilon t) = M-T <$> synthType Î“ t

synthType' Î“ (Ev m t) = do
  T â† traversePrimMetaArgs (synthType Î“) t
  appendIfError
    (primMetaArgsSequence $ primMetaArgsZipWith (checkÎ²Î· Î“) T $ primMetaS m)
    ("The arguments for primitive" <+> show m <+> "have incorrect types!")
  return $ M-T $ primMetaT m t

synthType' Î“ (Gamma t tâ‚) = do
  T â† synthType Î“ t
  Tâ‚ â† synthType Î“ tâ‚
  case (hnfNorm Î“ T) of Î»
    { (M-T u) â†’ do
      appendIfError (checkÎ²Î· Î“ Tâ‚ (Pi "" (FreeVar "init$err") (weakenBy 1 $ M-T u)))
        ("The second term supplied to CatchErr has type" <+> show Tâ‚ +
         ", while it should have type 'init$err â†’ M" <+> show u)
      return $ M-T u
    ; _ â†’ throwError "The first term in CatchErr needs to have type 'M t' for some 't'" }

synthType' Î“ (Char-T c) = return (Const-T CharT)
synthType' Î“ (CharEq t t') = do
  T â† synthType Î“ t
  T' â† synthType Î“ t'
  case (hnfNorm Î“ T) of Î»
    { (Const-T CharT) â†’ case (hnfNorm Î“ T') of Î»
      { (Const-T CharT) â†’ return $ FreeVar "Bool"
      ; _ â†’ throwError "The second term in CharEq needs to have type Char" }
    ; _ â†’ throwError "The first term in CharEq needs to have type Char" }
